apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionCode 12
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Configure only for each module that uses Java 8 language features (either in its source code or through
    // dependencies).
    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13-beta-3'

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.interpolator:interpolator:1.0.0"
    implementation "androidx.core:core:1.0.2"
    api 'com.github.addisonElliott:RippleDrawable:3.0.0'
}

// build a jar with source files
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

afterEvaluate {
    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/SegmentedButton"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                groupId = 'com.lisb.android.segmentedbutton'
                artifactId = 'segmentedbutton'
                version = '3.1.10'
            }
        }
    }
}
